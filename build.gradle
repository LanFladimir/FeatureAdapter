// Top-level build file where you can add configuration options common to all sub-projects/modules.
apply from: rootProject.file('config/hooks/install-git-hooks.gradle')

buildscript {
  ext.versions = ['minSdk'        : 14,
                  'compileSdk'    : 26,
                  'buildTools'    : '26.0.2',
                  'supportLibrary': '26.1.0',]

  ext.deps = [javaxannotation: 'javax.annotation:javax.annotation-api:1.2',
              findbugs       : 'com.google.code.findbugs:jsr305:2.0.1',
              junit          : 'junit:junit:4.12',
              easymock       : 'org.easymock:easymock:3.4',
              rxjava         : 'io.reactivex:rxjava:1.2.7',
              rxbinding      : 'com.jakewharton.rxbinding:rxbinding:1.0.1',

              'support'      : ['compat': "com.android.support:support-compat:${versions.supportLibrary}",
                                'design': "com.android.support:design:${versions.supportLibrary}",
                                'recyclerview': "com.android.support:recyclerview-v7:${versions.supportLibrary}"],]

  repositories {
    jcenter()
    google()
    maven {
      url "http://nexus-dev.snc1/content/repositories/releases/"
    }
    maven {
      url "https://plugins.gradle.org/m2/"
    }
  }

  dependencies {
    classpath 'com.groupon.gradle:android-maven-publish:2.0.2'
    classpath 'com.groupon.gradle:nexus-publish:2.0.3'
    classpath 'com.android.tools.build:gradle:3.0.0-rc1'
    classpath "gradle.plugin.nl.javadude.gradle.plugins:license-gradle-plugin:0.14.0"
    classpath "net.ltgt.gradle:gradle-errorprone-plugin:0.0.11"
  }
}

allprojects {
  group = GROUP
  version = VERSION_NAME

  repositories {
    jcenter()
    google()
    maven {
      url "http://nexus-dev.snc1/content/repositories/snapshots/"
    }
    maven {
      url "http://nexus-dev.snc1/content/repositories/releases/"
    }
  }

  ext {
    // plugin for all (checkstyle, pmd and findbugs)
    quality_gradle_java_file = "config/quality_java.gradle"
    quality_gradle_android_file = "config/quality_android.gradle"

    // config files
    pmd_rulesetFile = "${project.rootDir}/config/pmd/pmd-ruleset.xml"
  }

  apply plugin: 'com.github.hierynomus.license'

  task licenseFormatSrc (type:nl.javadude.gradle.plugins.license.License) {
    source = files("src/main/java", "src/test/java")
  }
  licenseFormat.dependsOn licenseFormatSrc

  license {
    header = rootProject.file('config/license/LICENSE')
    mapping {
      java = 'SLASHSTAR_STYLE'
    }
    exclude "**/*.xml"
  }
}

task clean(type: Delete) {
  delete rootProject.buildDir
}

